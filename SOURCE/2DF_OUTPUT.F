      PROGRAM TWOD_OUTPUT
C
C     WRITTEN BY: MATTHEW STADELMAN
C
C     PROGRAM DESCRIPTON: RUNS THE LOCAL CUBIC LAW MODEL ON A SUPPLIED
C         FRACTURE APERTURE MAP OUTPUTTING THE DATA TO MULTIPLE CSV FILES
C         AND A VTK FILE FOR USE WITH PARAVIEW. THIS PROGRAM WAS DESIGNED
C         UNDER AN ORISE APPOINTMENT AT THE NATIONAL ENERGY TECHNOLOGY LAB 
C         IN MORGANTOWN WEST VIRGINIA.
C
C     DATE WRITTEN:  2015/03/02 
C     LAST MODIFIED: 2015/02/08 
C
C     SUBROUTINE CALLS: INITIALIZE_RUN, FRAC_FLOW, PS_TO_PR,
C                       CALC_PERCENTILE 
C
C ---------------------------------------------------------------------
C     VARIABLE DESCRIPTION:
C       Q(1:4,:) - FLOW THROUGH THE SIDES OF A CELL IN ORDER: LEFT,RIGHT,TOP,BOTTOM
C       Q(5,:) - STORES THE FINAL NET MASS GAIN/LOSS FOR A CELL
C       PQX_OUT - X FLOW DATA USED FOR VECTOR PLOTTING  
C       PQZ_OUT - Z FLOW DATA USED FOR VECTOR PLOTTING 
C       PQZ_OUT - FLOW MAGNITUDE DATA USED FOR FLOW MAP AND REYNOLDS CALCULATION   
C       REN - ARRAY THAT STORES THE ESTIMATED REYNOLDS NUMBER FOR A CELL
C       RES - ARRAY THAT STORES NORMALIZED MASS/GAIN LOSSES FOR A CELL
C       FLOW - TOTAL FLOW OUT OF THE FRACTURE
C       NFQ - 1-D EQUIVALENT FLOW
C       FLOW_CF - REPRESENTS THE DISCREPANCY BETWEEN CUBIC LAW AND LOCAL CUBIC LAW (Q LCL)/(Q CL)
C       AVG_CORR - AVERAGE OF X AND Z CORRELTION LENGTHS, USED IN CALCULATIONS
C       BULK_REN - OVERALL REYNOLDS NUMBER FOR THE FRACTURE
C       HOM_TRANS - AVERAGE FRACTURE TRANSMISSIBILITY
C       X,Y,Z - POINT COORDINATES USED TO CREATE VTK MESH
C       BOK - CONTROL VARIABLE USED TO HALT PROGRAM IF A FATAL ERROR IS ENCOUNTERED
C
C ----------------------------------------------------------------------
C   
      USE TWOD_MODULE 
C      
      IMPLICIT NONE
      REAL(8), ALLOCATABLE :: Q(:,:)
      REAL(8), ALLOCATABLE :: PQX_OUT(:),PQZ_OUT(:),PQM_OUT(:)
      REAL(8), ALLOCATABLE :: REN(:),RES(:)
      REAL(8) :: FLOW,NFQ,AVG_CORR,BULK_REN,HOM_TRANS
      REAL(8) :: FLOW_CF,X,Y,Z
      INTEGER :: I,J,N,IX,IZ,IC,LB  
      LOGICAL :: BOK
C
C     INITIALIZING THE MODEL 
      BOK   = .TRUE.
      CALL INITIALIZE_RUN(BOK)
      IF (.NOT. BOK) GOTO 1000
C
C     INITIALIZING VARIABLES 
      LB = NX*NZ 
      N = 5 !NUMBER OF FLOW VALUES TO CALCULATE
      ALLOCATE(Q(N,LB),PQX_OUT(LB),PQZ_OUT(LB),PQM_OUT(LB))
      ALLOCATE(RES(LB),REN(LB))
C
C     RUNNING SUBROUTINE TO SOLVE FOR FLOW 
      CALL BLANK
      CALL MESSAGE(" CALCULATING FLOW THROUGH FRACTURE")
      IF (RATEC) THEN
        CALL FRAC_FLOW(Q,N,BOK)
        IF (.NOT. BOK) GOTO 1000
        CALL BLANK
        CALL MESSAGE("     INTERMEDIATE FRACTURE FLOWS CALCULATED")
C
C       CALCULATING INTERMEDIATE VALUES TO CORRECT BOUNDARY PRESSURE 
C       FOR INHOMOGENEOUS APERTURE 
        CALL BLANK
        WRITE(CVAR,600)"INTERMEDIATE",OUTPB*PRES_CONV,TRIM(UNIT_OUT(1))
        CALL MESSAGE(CVAR)
C
        FLOW = 0.0
        DO IC = 1,NC
          I = PBCELLS(IC)
          IF (INDEX(OUTFLOW,'LEFT') > 0)   FLOW = FLOW + Q(1,I)
          IF (INDEX(OUTFLOW,'RIGHT') > 0)  FLOW = FLOW + Q(2,I)
          IF (INDEX(OUTFLOW,'BOTTOM') > 0) FLOW = FLOW + Q(3,I)
          IF (INDEX(OUTFLOW,'TOP') > 0)    FLOW = FLOW + Q(4,I)
        END DO
        FLOW = ABS(FLOW)
        WRITE(CVAR,605)"INTERMEDIATE",FLOW*RATE_CONV*STD_CONV,
     &                               TRIM(UNIT_OUT(5))
        CALL MESSAGE(CVAR)
C
C       SETTING UP SECOND RUN
        FRAC_PS(:,:) = FPSB 
        !
        ! ADJUSTING PRESSURE BOUNDARY
        IF (INDEX(OUTFLOW,'LEFT') > 0) THEN
          FLOW_CF = ABS(FLOW/(FPSB-OUTPSB)/AVG_TRX)
          OUTRATE = OUTRATE/FLOW_CF
          OUTPSB = FPSB - OUTRATE/AVG_TRX
          DO IZ = 1,NZ
            FRAC_PS(IZ,0) = OUTPSB
          END DO
        ELSE IF (INDEX(OUTFLOW,'RIGHT') > 0) THEN  
          FLOW_CF = ABS(FLOW/(FPSB-OUTPSB)/AVG_TRX)
          OUTRATE = OUTRATE/FLOW_CF
          OUTPSB = FPSB - OUTRATE/AVG_TRX
          DO IZ = 1,NZ
            FRAC_PS(IZ,NX+1) = OUTPSB
          END DO
        ELSE IF (INDEX(OUTFLOW,'TOP') > 0) THEN
          FLOW_CF = ABS(FLOW/(FPSB-OUTPSB)/AVG_TRZ)
          OUTRATE = OUTRATE/FLOW_CF
          OUTPSB = FPSB - OUTRATE/AVG_TRZ
          DO IX = 1,NX
            FRAC_PS(NZ+1,IX) = OUTPSB
          END DO
        ELSE IF (INDEX(OUTFLOW,'BOTTOM') > 0) THEN 
          FLOW_CF = ABS(FLOW/(FPSB-OUTPSB)/AVG_TRZ)
          OUTRATE = OUTRATE/FLOW_CF
          OUTPSB = FPSB - OUTRATE/AVG_TRZ
          DO IX = 1,NX
            FRAC_PS(0,IX) = OUTPSB
          END DO
        ELSE 
          GOTO 1000
        END IF
C
        WRITE(CVAR,620)FLOW_CF
        CALL MESSAGE(CVAR)
        WRITE(CVAR,640)Q(5,MAXLOC(ABS(Q(5,1:LB))))   
        CALL MESSAGE(CVAR)
        OUTPB = OUTPSB
        IF (IFLUID == 1) CALL PS_TO_PR(OUTPB,OUTPSB,BOK)
        IF (.NOT. BOK) GOTO 1000    
C
      END IF
C
      CALL FRAC_FLOW(Q,N,BOK)
      IF (.NOT. BOK) GOTO 1000
      CALL BLANK
      CALL MESSAGE("     FINAL FRACTURE FLOWS CALCULATED")
C
C
C ############## CALCULATION OF OUTPUT DATA BEGINS HERE ###################
C
C     CALCULATION OF FLOW RATE FROM FRACTURE
      NFQ = 0.0
      FLOW = 0.0
      DO IC = 1,NC
        I = PBCELLS(IC)
        IF (INDEX(OUTFLOW,'LEFT') > 0)   FLOW = FLOW + Q(1,I)
        IF (INDEX(OUTFLOW,'RIGHT') > 0)  FLOW = FLOW + Q(2,I)
        IF (INDEX(OUTFLOW,'TOP') > 0)    FLOW = FLOW + Q(3,I)
        IF (INDEX(OUTFLOW,'BOTTOM') > 0) FLOW = FLOW + Q(4,I)
      END DO
      FLOW = ABS(FLOW)
C
C     CALCULATING FLOW CORRECTION FACTOR
C     AND 1-D EQUIVALENT FLOW
      IF ((INDEX(OUTFLOW,'RIGHT')+INDEX(OUTFLOW,'LEFT')) > 0) THEN
        FLOW_CF =   ABS(FLOW/(FPSB-OUTPSB)/AVG_TRX)
        NFQ =       ABS(AVG_TRX*(FPSB-OUTPSB)*STD_CONV)
        HOM_TRANS = ABS(FLOW/(FPSB-OUTPSB))*AVG_VISC*DIAM
        BULK_REN = (2*RHO*FLOW)/(AVG_VISC*LENG)
      ELSEIF ((INDEX(OUTFLOW,'TOP')+INDEX(OUTFLOW,'BOTTOM')) > 0) THEN 
        FLOW_CF =   ABS(FLOW/(FPSB-OUTPSB)/AVG_TRZ)
        NFQ =       ABS(AVG_TRZ*(FPSB-OUTPSB)*STD_CONV)
        HOM_TRANS = ABS(FLOW/(FPSB-OUTPSB))*AVG_VISC*LENG
        BULK_REN = (2*RHO*FLOW)/(AVG_VISC*DIAM)
      END IF
      FLOW = FLOW*STD_CONV
C
      WRITE(CVAR,600)"FINAL",OUTPB*PRES_CONV,TRIM(UNIT_OUT(1))
      CALL MESSAGE(CVAR)
      WRITE(CVAR,605)"FINAL",FLOW*RATE_CONV,TRIM(UNIT_OUT(5))
      CALL MESSAGE(CVAR)
      WRITE(CVAR,640)Q(5,MAXLOC(ABS(Q(5,1:LB))))    
      CALL MESSAGE(CVAR)    
C
C     CONVERTING VALUES TO OUTPUT UNITS
      AP_MAP = AP_MAP*DIM_CONV
      FRAC_PR = FRAC_PR*PRES_CONV
      FPB = FPB*PRES_CONV
      OUTPB = OUTPB*PRES_CONV
      DX = DX*DIM_CONV
      DZ = DZ*DIM_CONV
      DIAM = DIAM*DIM_CONV
      LENG = LENG*DIM_CONV
      WIDTH = WIDTH*DIM_CONV 
      AVG_APER = AVG_APER*DIM_CONV
      APER_RMS = APER_RMS*DIM_CONV
      APER_DEV = APER_DEV*DIM_CONV
      Z_CORR = Z_CORR*DIM_CONV
      X_CORR = X_CORR*DIM_CONV
      AVG_CORR = (2*X_CORR*Z_CORR)/(X_CORR+Z_CORR)
      FLOW = FLOW*RATE_CONV
      NFQ = NFQ*RATE_CONV
      ! NORMALIZING RESIDUAL ARRAY
      RES = ABS(Q(5,:))
      RES = RES/MAXVAL(RES)
      !
      ! DEFAULT DIRECTIONS ARE RIGHT AND TOP
      PQX_OUT = Q(2,:)*RATE_CONV*STD_CONV
      PQZ_OUT = Q(4,:)*RATE_CONV*STD_CONV
      ! CHECKING IF OUTFLOW IS REVERSED 
      IF (INDEX(OUTFLOW,'LEFT') > 0) THEN
        PQX_OUT = Q(1,:)*RATE_CONV*STD_CONV 
      ELSEIF (INDEX(OUTFLOW,'BOTTOM') > 0) THEN
        PQZ_OUT = Q(3,:)*RATE_CONV*STD_CONV
      END IF
C
C     CALCULATING FLOW MAGNITUDE AND ESTIMATED REYNOLDS NUMBER
      DO I = 1,LB
        PQM_OUT(I) = SQRT(PQX_OUT(I)**2+PQZ_OUT(I)**2)
        REN(I) = (RHO*PQM_OUT(I)/RATE_CONV)/(AVG_VISC*WIDTH(I)/DIM_CONV)
      END DO
C
C     CALCULATING FLOW PERCENTILES
      IF (PERCENTILE) THEN
        CALL CALC_PERCENTILE(PQX_OUT,PERC_ARR,NX*NZ,NPCT)
        CALL BLANK
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2200) 'X-FLOW'
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2225)'PERCENTILE','['//TRIM(UNIT_OUT(5))//']'
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        DO I = 1,NPCT
          WRITE(CVAR,2230)PERC_ARR(1,I),PERC_ARR(2,I)
          CALL MESSAGE(CVAR)
        END DO
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        !
        CALL CALC_PERCENTILE(PQZ_OUT,PERC_ARR,NX*NZ,NPCT)
        CALL BLANK
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2200) 'Z-FLOW'
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2225)'PERCENTILE','['//TRIM(UNIT_OUT(5))//']'
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        DO I = 1,NPCT
          WRITE(CVAR,2230)PERC_ARR(1,I),PERC_ARR(2,I)
          CALL MESSAGE(CVAR)
        END DO
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        !
        CALL CALC_PERCENTILE(PQM_OUT,PERC_ARR,NX*NZ,NPCT)
        CALL BLANK
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2200) 'FLOW MAGNITUDE'
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2225)'PERCENTILE','['//TRIM(UNIT_OUT(5))//']'
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        DO I = 1,NPCT
          WRITE(CVAR,2230)PERC_ARR(1,I),PERC_ARR(2,I)
          CALL MESSAGE(CVAR)
        END DO
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        !
        CALL CALC_PERCENTILE(RES,PERC_ARR,NX*NZ,NPCT)
        CALL BLANK
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2200) 'NORMALIZED RESIDUAL'
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2225)'PERCENTILE','[-]'
        CALL MESSAGE(CVAR)
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
        DO I = 1,NPCT
          WRITE(CVAR,2230)PERC_ARR(1,I),PERC_ARR(2,I)
          CALL MESSAGE(CVAR)
        END DO
        WRITE(CVAR,2210)
        CALL MESSAGE(CVAR)
      END IF
C
      CALL BLANK
      CALL MESSAGE(" WRITING DATA TO OUTPUT FILES...")
C
C     OUTPUTTING DATA TO STATS FILE
      WRITE(ISTAT,650) TRIM(UNIT_OUT(1)),TRIM(UNIT_OUT(1))
      WRITE(ISTAT,"(2(G20.9,',',:))") FPB,OUTPB
C
      WRITE(ISTAT,"(A)")'#'
      WRITE(ISTAT,655)TRIM(UNIT_OUT(2)),TRIM(UNIT_OUT(2)),
     &      TRIM(UNIT_OUT(2)),TRIM(UNIT_OUT(2)),TRIM(UNIT_OUT(2))
      WRITE(ISTAT,"(*(F18.6,',',:))")APER_RMS,APER_DEV,X_CORR,Z_CORR,
     &                               AVG_CORR
C
      WRITE(ISTAT,"(A)")'#'
      WRITE(ISTAT,660)
      WRITE(ISTAT,"(3(F18.6,',',:))")APER_DEV/AVG_APER,
     &      APER_DEV/AVG_CORR,AVG_APER/AVG_CORR
C
      WRITE(ISTAT,"(A)")'#'
      WRITE(ISTAT,665)
      WRITE(ISTAT,"(F10.6,',',E15.9,',',G15.9)")BULK_REN,HOM_TRANS,
     &     (FPB-OUTPB)/PRES_CONV
C
      WRITE(ISTAT,"(A)")'#'
      WRITE(ISTAT,700)TRIM(UNIT_OUT(5)),TRIM(UNIT_OUT(5))
      WRITE(ISTAT,701)FLOW,NFQ,FLOW_CF*100
C
C     OUTPUTTING THE WIDTH VECTOR USED
      DO IZ = 0,NZ-1
        I = IZ*NX+1
        J = IZ*NX+NX
        WRITE(IMAP,"(100000(G15.8,',',:))") WIDTH(I:J)
      END DO
      CALL MESSAGE("     ADJUSTED APERTURE FILE COMPLETED")
C
C     OUTPUTTING QX, QZ AND MAG VALUES 
      DO IZ = 0,NZ-1
        I = IZ*NX+1
        J = IZ*NX+NX
        WRITE(IFLOX,"(100000(G15.8,',',:))") PQX_OUT(I:J)
      END DO
C
      DO IZ = 0,NZ-1
        I = IZ*NX+1
        J = IZ*NX+NX
        WRITE(IFLOZ,"(100000(G15.8,',',:))") PQZ_OUT(I:J)
      END DO
C
      DO IZ = 0,NZ-1
        I = IZ*NX+1
        J = IZ*NX+NX
        WRITE(IFLOM,"(100000(G15.8,',',:))") PQM_OUT(I:J)
      END DO
      CALL MESSAGE("     FLOW FILE COMPLETED")
C
C     OUTPUTTING THE FRAC_PRESSURE
      DO IZ = 1,NZ
        WRITE(IPRES,"(100000(G15.8,',',:))") FRAC_PR(IZ,1:NX)
      END DO
      CALL MESSAGE("     PRESSURE FILE COMPLETED")
C
C     CREATING THE VTK HEADERS 
      CALL BLANK
      CALL MESSAGE("     CREATING THE VTK FILE...")
      WRITE(IVTK,"('# vtk DataFile Version 3.0')")
      WRITE(IVTK,"('vtk output')")
      WRITE(IVTK,"('ASCII')")
      WRITE(IVTK,"('DATASET STRUCTURED_GRID')")
      WRITE(IVTK,"('DIMENSIONS ',I0,1X,I0,1X,I0)")NX+1,NZ+1,2
      WRITE(IVTK,"('POINTS ',I0,' float')")(NX+1)*(NZ+1)*2
C
C     OUPUTTING SURFACE POINTS FOR THE GRID
      DO N = -1,1,2
        Z = 0.0
        DO IZ = 1,NZ
          X = 0.0
          Y = N*AP_MAP(IZ,1,1)/2.0
          WRITE(IVTK,"(3(F0.6,' ',:))")X,Y,Z
          DO IX = 1,NX
            I = (IZ - 1)*NX + IX
            X = X + DX(I)
            Y = N*AP_MAP(IZ,IX,2)/2.0
            WRITE(IVTK,"(3(F0.6,' ',:))")X,Y,Z
          END DO
          Z = Z + DZ(I)
        END DO
        !
        X = 0.0
        Y = N*AP_MAP(NZ,1,4)/2.0
        WRITE(IVTK,"(3(F0.6,' ',:))")X,Y,Z
        DO IX = 1,NX
          I = (NZ - 1)*NX + IX
          X = X + DX(I)
          Y = N*AP_MAP(NZ,IX,3)/2.0
          WRITE(IVTK,"(3(F0.6,' ',:))")X,Y,Z
        END DO
      END DO
      CALL MESSAGE("         VTK POINTS OUTPUT TO FILE")  
C
C     OUTPUTTING DATA INTO THE VTK FILE FOR PARAVIEW 
      CALL BLANK
      CALL MESSAGE("     OUTPUTTING DATA TO THE VTK FILE...")
C
C     CREATING THE DATA TABLES
      WRITE(IVTK,"(' ')")
      WRITE(IVTK,"('CELL_DATA ',I0)")NX*NZ*1
      WRITE(IVTK,"('SCALARS APERTURE[',A,'] float')")TRIM(UNIT_OUT(2))
      WRITE(IVTK,"('LOOKUP_TABLE default')")
      DO IZ = 1,NZ
        DO IX = 1,NX
          I = (IZ - 1)*NX + IX
          WRITE(IVTK,"(3(F0.6,' ',:))")WIDTH(I)
        END DO
      END DO
      CALL MESSAGE("         APERTURE DATA OUTPUT TO VTK FILE")
C
      WRITE(IVTK,"(' ')")
      WRITE(IVTK,"('SCALARS PRESSURE[',A,'] float')")TRIM(UNIT_OUT(1))
      WRITE(IVTK,"('LOOKUP_TABLE default')")
      DO IZ = 1,NZ
        DO IX = 1,NX
          WRITE(IVTK,"(3(F0.6,' ',:))")FRAC_PR(IZ,IX)
        END DO
      END DO
      CALL MESSAGE("         PRESSURE DATA OUTPUT TO VTK FILE")
C
      WRITE(IVTK,"(' ')")
      WRITE(IVTK,"('SCALARS REYNOLDS_NUMBER[] float')")
      WRITE(IVTK,"('LOOKUP_TABLE default')")
      DO IZ = 1,NZ
        DO IX = 1,NX
          I = (IZ - 1)*NX + IX
          WRITE(IVTK,"(3(F0.6,' ',:))")REN(I)
        END DO
      END DO
      CALL MESSAGE("         REYNOLDS NUMBER OUTPUT TO VTK FILE")
C
      WRITE(IVTK,"(' ')")
      WRITE(IVTK,"('SCALARS NORMALIZED_RESIDUALS[] float')")
      WRITE(IVTK,"('LOOKUP_TABLE default')")
      DO IZ = 1,NZ
        DO IX = 1,NX
          I = (IZ - 1)*NX + IX
          WRITE(IVTK,"(3(F0.6,' ',:))")RES(I)
        END DO
      END DO
      CALL MESSAGE("         NORMALIZED RESIDUALS OUTPUT TO VTK FILE")
C
      WRITE(IVTK,"(' ')")
      WRITE(IVTK,"('VECTORS FLOW[',A,'] float')")TRIM(UNIT_OUT(5))
      DO IZ = 1,NZ
        DO IX = 1,NX
          I = (IZ - 1)*NX + IX
          WRITE(IVTK,"(3(F0.6,' ',:))")PQX_OUT(I),0.0,PQZ_OUT(I)
        END DO
      END DO
      CALL MESSAGE("         VECTOR FLOW DATA OUTPUT TO VTK FILE")      
C  
C     DEALLOCATING FRACTURE PROPERTIES 
      DEALLOCATE(PBCELLS,FRAC_PR,TRX,TRZ)    
C     DEALLOCATING FLOW VECTORS 
      DEALLOCATE(Q,PQX_OUT,PQZ_OUT,WIDTH,DX,DZ)          
C     DEALLOCATING PVT ARRAYS
      DEALLOCATE(PR,VI) 
C      
      CALL BLANK
      CALL MESSAGE(" DEALLOCATED ALL ARRAYS")
C
C     CLOSING OUTPUT FILES
      CLOSE(ISTAT)
      CLOSE(IMAP)
      CLOSE(IFLOX)
      CLOSE(IFLOZ)
      CLOSE(IFLOM)
      CLOSE(IPRES)
      CLOSE(IVTK)
C
      CALL BLANK
      CALL MESSAGE(" OUTPUT FILES CLOSED")
      CLOSE(IOUT)  
C     
      STOP
C
  600 FORMAT(5X,A,' BOUNDARY PRESSURE: ',3X,G20.9,1X,A)
C
  605 FORMAT(5X,A,' FLOW RATE CALCULATED: ',G20.9,1X,A)
C
  620 FORMAT(5X,'FLOW CORRECTION FACTOR USED: ',F13.9,1X,A)
C
  640 FORMAT(5X,'MAXIMUM NET FLOW RESIDUAL: ',G20.9,1X,A)
C
  650 FORMAT(1X,'CONFINING PRES [',A,'],OUTLET PRES [',A,']')
C
  655 FORMAT(1X,'APERTURE RMS [',A,'],APERTURE STD DEV [',A,
     &'],X - CORRELATION LENGTH [',A,'],Z - CORRELATION LENGTH [',A,
     &'],AVG - CORRELATION LENGTH [',A,']')
C
  660 FORMAT(1X,'STD DEV / MEAN APER [-],STD DEV / AVG CORR LENGTH [-],'
     &          ,' MEAN APER / AVG CORR LENGTH [-]')
C
  665 FORMAT(1X,'BULK REYNOLDS # [-],AVERAGE TRANSMISSIBILTY [M^4],',
     &           'PRESSURE DIFFERENTIAL [PA]')
C
  700 FORMAT(1X,'2-D FLOW [',A,'],1-D EQIV [',A,'],% OF 1-D [-]')
C    
  701 FORMAT(4(G18.9,',',:))
C
 1000 CALL BLANK
      CALL MESSAGE('     **** EXITING PROGRM ****')
C
C
 2200 FORMAT(9X,A,1X,'RANGES')
C
 2210 FORMAT(9X,32('-'))
C
 2225 FORMAT(1X,A18,3X,'|',A17)
C
 2230 FORMAT(9X,'|',2X,F7.3,3X,'|',1X,F15.6,1X,'|')
      END PROGRAM
      
     
